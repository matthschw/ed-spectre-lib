//* (C) Copyright Efficient Power Conversion Corporation. All rights reserved.
//*****************************************************************************
//* Version History:
//* 1.00: 01/14/2019 - Initial Model Creation
//* 1.01: 12/14/2020 - Updated the Model from the Preliminary Version

simulator lang=spectre

subckt EPC2055 (gatein drainin sourcein)
parameters  si=7.014e-01 so=3.237e+00 sr=1.079e+03
parameters  aWg=1079 A1=(2.607e-02*aWg) k2=2.095e+00 k3=9.000e-02 rpara=1.844e-03
+	rpara_s_factor=3.291e-01 aITc=3.000e-03 arTc=-7.000e-03 k2Tc=6.000e-04
+	x0_0=4.732e+00 x0_0_TC=-3.000e-03 x0_1=-4.118e-01 x0_1_TC=0.000e+00
+	dgs1=4.3e-07 dgs2=2.6e-13 dgs3=0.8 dgs4=0.23
+	ags1=(1.187e-09*si) ags2=(4.600e-10*si) ags3=1.730e+00 ags4=2.766e-01
+	ags5=(0.000e+00*si) ags6=-4.571e+01 ags7=1.657e+00
+	agd1=(1.854e-15*sr) agd2=(9.339e-14*sr) agd3=-1.360e+00 agd4=2.986e+00
+	agd5=(1.668e-14*sr) agd6=-5.260e+00 agd7=2.616e+01 agd8=(2.396e-14*sr)
+	agd9=-1.154e+01 agd10=1.429e+00
+	asd1=(9.734e-11*so) asd2=(9.710e-11*so) asd3=-7.496e+00 asd4=1.233e+01
+	asd5=(3.763e-11*so) asd6=-1.401e+01 asd7=8.496e-01 asd8=(4.693e-11*so)
+	asd9=-1.147e+01 asd10=1.684e+00
+	rg_value=0.4

rd (drainin drain) resistor r=((1-rpara_s_factor)*rpara*(1-arTc*(temp-25)))
rs (sourcein source) resistor r=(rpara_s_factor*rpara*(1-arTc*(temp-25)))
rg (gatein gate) resistor r=(rg_value)

rcsdconv (drain source) resistor r=(1E9/aWg)
rcgsconv (gate source) resistor r=(1E9/aWg)
rcgdconv (gate drain) resistor r=(1E9/aWg)

gswitch drain source bsource i=( (v(drain,source)>0) ? 
+	(A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,source)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(drain,source)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,source))*v(drain,source))) :
+	(-A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,drain)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(source,drain)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,drain))*v(source,drain))) )

ggsdiode gate source bsource i=( (v(gate,source)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,source))/dgs3)-1)+dgs2*(exp((v(gate,source))/dgs4)-1))) )

ggddiode gate drain bsource i=( (v(gate,drain)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,drain))/dgs3)-1)+dgs2*(exp((v(gate,drain))/dgs4)-1))) )


C_GS  (gate source) bsource c=(ags1)  
G_CGS1 (gate source) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,source)-ags3)/ags4))+
+	ags5*ags7*log(1+exp((v(source,drain)-ags6)/ags7)) )

C_GD  (gate drain) bsource c=(agd1)  
G_CGD1 (gate drain) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,drain)-ags3)/ags4))+
+	agd2*agd4*log(1+exp((v(gate,drain)-agd3)/agd4))+
+	agd5*agd7*log(1+exp((v(gate,drain)-agd6)/agd7))+
+	agd8*agd10*log(1+exp((v(gate,drain)-agd9)/agd10)) )

C_SD  (source drain) bsource c=(asd1) 
G_CSD1 (source drain) bsource q=(asd2*asd4*log(1+exp((v(source,drain)-asd3)/asd4))+
+	asd5*asd7*log(1+exp((v(source,drain)-asd6)/asd7))+ 
+	asd8*asd10*log(1+exp((v(source,drain)-asd9)/asd10)) )

ends EPC2055
