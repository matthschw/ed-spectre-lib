//* (C) Copyright Efficient Power Conversion Corporation. All rights reserved.
//*****************************************************************************
//* Version History:
//* 1.00: 09/23/2013 - Initial Model Creation
//* 1.01: 03/17/2014 - Adjusted braces to correct syntax errors
//* 1.02: 09/29/2015 - Updated the model from the preliminary version
//* 1.03: 05/28/2019 - Fixed a typo

simulator lang=spectre

subckt EPC8002 (gatein drainin sourcein) 
parameters aWg=14 A1=0.59557 k2=2.4645 k3=0.15 rpara=0.27298 rpara_s_factor=0.29 
 + aITc=0.00325 arTc=-0.0065 k2Tc=0.00088 x0_0=1.1371 x0_1=8.2967e-07 x0_1_TC=0 
 + dgs1=4.3e-07 dgs2=2.6e-13 dgs3=0.8 dgs4=0.23 
 + ags1=1.9424e-11 ags2=3.7971e-12 ags3=2.0159 ags4=0.20705 
 + ags5=-1.2089e-13 ags6=-4.288 ags7=2.6466 
 + agd1=2.7976e-15 agd2=1.7743e-12 agd3=-0.065681 agd4=3.2062 
 + agd5=5.6261e-13 agd6=-13.247 agd7=5.2535 
 + agd8=1.3059e-13 agd9=-56.273 agd10=20.505 
 + asd1=5.1501e-12 asd2=4.0613e-12 asd3=-19.4 asd4=3.4482 
 + asd5=6.5483e-12 asd6=-0.21194 asd7=23.462 rg_value=0.3 

rd (drainin drain) resistor r=((1-rpara_s_factor)*rpara*(1-arTc*(temp-25)))
rs (sourcein source) resistor r=(rpara_s_factor*rpara*(1-arTc*(temp-25)))
rg (gatein gate) resistor r=(rg_value)

rcsdconv (drain source) resistor r=(1E9/aWg)
rcgsconv (gate source) resistor r=(1E9/aWg)
rcgdconv (gate drain) resistor r=(1E9/aWg)

gswitch drain source bsource i=( (v(drain,source)>0) ? 
+	(A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,source)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(drain,source)/(1 + max(x0_0+x0_1*(1-x0_1_TC*(temp-25))*v(gate,source),0.2)*v(drain,source)) ) : 
+	(-A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,drain)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(source,drain)/(1 + max(x0_0+x0_1*(1-x0_1_TC*(temp-25))*v(gate,drain),0.2)*v(source,drain)) ) )

ggsdiode gate source bsource i=( (v(gate,source)>10) ? 
+	(0.5*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.5*aWg/1077*(dgs1*(exp((v(gate,source))/dgs3)-1)+dgs2*(exp((v(gate,source))/dgs4)-1)))  )

ggddiode gate drain bsource i=( (v(gate,drain)>10) ? 
+	(0.5*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.5*aWg/1077*(dgs1*(exp((v(gate,drain))/dgs3)-1)+dgs2*(exp((v(gate,drain))/dgs4)-1))) )


C_GS  (gate source) bsource c=(ags1)  
G_CGS1 (gate source) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,source)-ags3)/ags4))+
+	ags5*ags7*log(1+exp((v(source,drain)-ags6)/ags7)) )

C_GD  (gate drain) bsource c=(agd1)  
G_CGD1 (gate drain) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,drain)-ags3)/ags4))+
+	agd2*agd4*log(1+exp((v(gate,drain)-agd3)/agd4))+
+	agd5*agd7*log(1+exp((v(gate,drain)-agd6)/agd7))+
+	agd8*agd10*log(1+exp((v(gate,drain)-agd9)/agd10)) )

C_SD  (source drain) bsource c=(asd1) 
G_CSD1 (source drain) bsource q=(asd2*asd4*log(1+exp((v(source,drain)-asd3)/asd4))+
+	asd5*asd7*log(1+exp((v(source,drain)-asd6)/asd7)) )

ends EPC8002
