//* (C) Copyright Efficient Power Conversion Corporation. All rights reserved.
//*****************************************************************************
//* Version History:
//* 1.00: 07/24/2015 - Initial Model Creation
//* 1.01: 04/03/2017 - Updated the Model from the Preliminary Version
//* 1.02: 05/28/2019 - Fixed a typo

simulator lang=spectre

subckt EPC2039 (gatein drainin sourcein) 
parameters aWg=330 A1=10.676 k2=2.48 k3=0.15 rpara=0.014026 rpara_s_factor=0.26 
 + aITc=0.0036 arTc=-0.006 k2Tc=0.0005 x0_0=1.6927 x0_1=1.6264e-05 x0_0_TC=-0.001 x0_1_TC=0 
 + dgs1=4.3e-07 dgs2=2.6e-13 dgs3=0.8 dgs4=0.23 
 + ags1=2.0713e-10 ags2=1.2151e-10 ags3=2.3682 ags4=0.17302 
 + ags5=-8.1438e-12 ags6=-11.988 ags7=1.4192 
 + agd1=6.5198e-15 agd2=4.0635e-12 agd3=-12.38 agd4=70.789 
 + agd5=3.5093e-11 agd6=-8.3958 agd7=4.1664 
 + agd8=7.5137e-13 agd9=-41.411 agd10=6.4114 
 + asd1=8.5372e-11 asd2=1.5843e-10 asd3=-0.68269 asd4=25.115 
 + asd5=9.8396e-11 asd6=-17.729 asd7=2.7426 rg_value=0.5 

rd (drainin drain) resistor r=((1-rpara_s_factor)*rpara*(1-arTc*(temp-25)))
rs (sourcein source) resistor r=(rpara_s_factor*rpara*(1-arTc*(temp-25)))
rg (gatein gate) resistor r=(rg_value)

rcsdconv (drain source) resistor r=(1E9/aWg)
rcgsconv (gate source) resistor r=(1E9/aWg)
rcgdconv (gate drain) resistor r=(1E9/aWg)

gswitch drain source bsource i=( (v(drain,source)>0) ? 
+	(A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,source)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(drain,source)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,source))*v(drain,source)) ) :
+	(-A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,drain)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(source,drain)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,drain))*v(source,drain)) ) )

ggsdiode gate source bsource i=( (v(gate,source)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,source))/dgs3)-1)+dgs2*(exp((v(gate,source))/dgs4)-1)))  )

ggddiode gate drain bsource i=( (v(gate,drain)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,drain))/dgs3)-1)+dgs2*(exp((v(gate,drain))/dgs4)-1))) )


C_GS  (gate source) bsource c=(ags1)  
G_CGS1 (gate source) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,source)-ags3)/ags4))+
+	ags5*ags7*log(1+exp((v(source,drain)-ags6)/ags7)) )

C_GD  (gate drain) bsource c=(agd1)  
G_CGD1 (gate drain) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,drain)-ags3)/ags4))+
+	agd2*agd4*log(1+exp((v(gate,drain)-agd3)/agd4))+
+	agd5*agd7*log(1+exp((v(gate,drain)-agd6)/agd7))+
+	agd8*agd10*log(1+exp((v(gate,drain)-agd9)/agd10)) )

C_SD  (source drain) bsource c=(asd1) 
G_CSD1 (source drain) bsource q=(asd2*asd4*log(1+exp((v(source,drain)-asd3)/asd4))+
+	asd5*asd7*log(1+exp((v(source,drain)-asd6)/asd7)) )

ends EPC2039
