//* (C) Copyright Efficient Power Conversion Corporation. All rights reserved.
//*****************************************************************************
//* Version History:
//* 1.00: 04/13/2022 - Initial Model Creation
//* 1.01: 06/14/2022 - Corrected Error in ends Statement

simulator lang=spectre

subckt EPC2066 (gatein drainin sourcein)
parameters  si=4.257e+03 so=4.257e+03 sr=4.257e+03
parameters  aWg=4257 A1=(1.708e-02*aWg) k2=2.228e+00 k3=9.000e-02 rpara=3.050e-04
+      rpara_s_factor=3.224e-01 aITc=4.200e-03 arTc=-5.184e-03 k2Tc=8.456e-04
+      x0_0=3.321e+00 x0_0_TC=-5.242e-03 x0_1=-2.000e-01 x0_1_TC=-1.000e-02
+      dgs1=4.3e-07 dgs2=2.6e-13 dgs3=0.8 dgs4=0.23
+      ags1=(7.346e-13*si) ags2=(3.054e-13*si) ags3=2.118e+00 ags4=2.319e-01
+      ags5=(0.000e+00*si) ags6=7.480e+01 ags7=1.055e+01
+      agd1=(3.641e-15*sr) agd2=(1.374e-13*sr) agd3=-3.472e-01 agd4=4.414e+00
+      agd5=(1.527e-14*sr) agd6=-1.348e+01 agd7=2.150e+00 agd8=(0.000e+00*sr)
+      agd9=-3.326e+01 agd10=8.900e-01
+      asd1=(3.249e-13*so) asd2=(3.259e-13*so) asd3=-1.435e+01 asd4=1.899e+00
+      asd5=(1.638e-13*so) asd6=-1.522e+01 asd7=6.350e+00 asd8=(0.000e+00*so)
+      asd9=-8.125e+01 asd10=5.665e+00
+      rg_value=0.4

rd (drainin drain) resistor r=((1-rpara_s_factor)*rpara*(1-arTc*(temp-25)))
rs (sourcein source) resistor r=(rpara_s_factor*rpara*(1-arTc*(temp-25)))
rg (gatein gate) resistor r=(rg_value)

rcsdconv (drain source) resistor r=(1E9/aWg)
rcgsconv (gate source) resistor r=(1E9/aWg)
rcgdconv (gate drain) resistor r=(1E9/aWg)

gswitch drain source bsource i=( (v(drain,source)>0) ? 
+	(A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,source)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(drain,source)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,source))*v(drain,source))) :
+	(-A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,drain)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(source,drain)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,drain))*v(source,drain))) )

ggsdiode gate source bsource i=( (v(gate,source)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,source))/dgs3)-1)+dgs2*(exp((v(gate,source))/dgs4)-1))) )

ggddiode gate drain bsource i=( (v(gate,drain)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,drain))/dgs3)-1)+dgs2*(exp((v(gate,drain))/dgs4)-1))) )


C_GS  (gate source) bsource c=(ags1)  
G_CGS1 (gate source) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,source)-ags3)/ags4))+
+	ags5*ags7*log(1+exp((v(source,drain)-ags6)/ags7)) )

C_GD  (gate drain) bsource c=(agd1)  
G_CGD1 (gate drain) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,drain)-ags3)/ags4))+
+	agd2*agd4*log(1+exp((v(gate,drain)-agd3)/agd4))+
+	agd5*agd7*log(1+exp((v(gate,drain)-agd6)/agd7))+
+	agd8*agd10*log(1+exp((v(gate,drain)-agd9)/agd10)) )

C_SD  (source drain) bsource c=(asd1) 
G_CSD1 (source drain) bsource q=(asd2*asd4*log(1+exp((v(source,drain)-asd3)/asd4))+
+	asd5*asd7*log(1+exp((v(source,drain)-asd6)/asd7))+ 
+	asd8*asd10*log(1+exp((v(source,drain)-asd9)/asd10)) )

ends EPC2066
