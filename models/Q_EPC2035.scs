//* (C) Copyright Efficient Power Conversion Corporation. All rights reserved.
//*****************************************************************************
//* Version History:
//* 1.00: 04/20/2015 - Initial Model Creation
//* 1.01: 05/28/2019 - Fixed a typo

simulator lang=spectre

subckt EPC2035 (gatein drainin sourcein) 
parameters aWg=159 A1=5.5332 k2=2.2219 k3=0.15 rpara=0.025065 rpara_s_factor=0.29 
 + aITc=0.004104 arTc=-0.0054 k2Tc=0.0006 x0_0=1.5473 x0_1=1.0199e-06 x0_1_TC=0 
 + dgs1=4.3e-07 dgs2=2.6e-13 dgs3=0.8 dgs4=0.23 
 + ags1=9.3156e-11 ags2=5.1011e-11 ags3=1.6846 ags4=0.19858 
 + ags5=-4.059e-14 ags6=-4.022 ags7=0.10106 
 + agd1=2.9762e-13 agd2=1.3222e-12 agd3=-0.11038 agd4=0.51625 
 + agd5=1.9347e-11 agd6=-2.3092 agd7=6.4081 
 + agd8=2.468e-12 agd9=-25.055 agd10=28.436 
 + asd1=3.7411e-11 asd2=4.5502e-11 asd3=-9.3759 asd4=8.3695 
 + asd5=7.8779e-11 asd6=-0.10977 asd7=22.842 rg_value=0.5 

rd (drainin drain) resistor r=((1-rpara_s_factor)*rpara*(1-arTc*(temp-25)))
rs (sourcein source) resistor r=(rpara_s_factor*rpara*(1-arTc*(temp-25)))
rg (gatein gate) resistor r=(rg_value)

rcsdconv (drain source) resistor r=(1E9/aWg)
rcgsconv (gate source) resistor r=(1E9/aWg)
rcgdconv (gate drain) resistor r=(1E9/aWg)

gswitch drain source bsource i=( (v(drain,source)>0) ? 
+	(A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,source)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(drain,source)/(1 + max(x0_0+x0_1*(1-x0_1_TC*(temp-25))*v(gate,source),0.2)*v(drain,source)) ) : 
+	(-A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,drain)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(source,drain)/(1 + max(x0_0+x0_1*(1-x0_1_TC*(temp-25))*v(gate,drain),0.2)*v(source,drain)) ) )

ggsdiode gate source bsource i=( (v(gate,source)>10) ? 
+	(0.5*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.5*aWg/1077*(dgs1*(exp((v(gate,source))/dgs3)-1)+dgs2*(exp((v(gate,source))/dgs4)-1)))  )

ggddiode gate drain bsource i=( (v(gate,drain)>10) ? 
+	(0.5*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.5*aWg/1077*(dgs1*(exp((v(gate,drain))/dgs3)-1)+dgs2*(exp((v(gate,drain))/dgs4)-1))) )


C_GS  (gate source) bsource c=(ags1)  
G_CGS1 (gate source) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,source)-ags3)/ags4))+
+	ags5*ags7*log(1+exp((v(source,drain)-ags6)/ags7)) )

C_GD  (gate drain) bsource c=(agd1)  
G_CGD1 (gate drain) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,drain)-ags3)/ags4))+
+	agd2*agd4*log(1+exp((v(gate,drain)-agd3)/agd4))+
+	agd5*agd7*log(1+exp((v(gate,drain)-agd6)/agd7))+
+	agd8*agd10*log(1+exp((v(gate,drain)-agd9)/agd10)) )

C_SD  (source drain) bsource c=(asd1) 
G_CSD1 (source drain) bsource q=(asd2*asd4*log(1+exp((v(source,drain)-asd3)/asd4))+
+	asd5*asd7*log(1+exp((v(source,drain)-asd6)/asd7)) )

ends EPC2035
