//* (C) Copyright Efficient Power Conversion Corporation. All rights reserved.
//*****************************************************************************
//* Version History:
//* 1.00: 04/04/2019 - Initial Model Creation

simulator lang=spectre

subckt EPC2214 (gatein drainin sourcein)
parameters  si=2.054e-01 so=1.517e+00 sr=3.160e+02
parameters  aWg=316 A1=(3.147e-02*aWg) k2=2.083e+00 k3=9.000e-02 rpara=1.167e-02
+	rpara_s_factor=2.400e-01 aITc=4.000e-03 arTc=-6.400e-03 k2Tc=6.000e-04
+	x0_0=2.736e+00 x0_0_TC=-1.000e-03 x0_1=1.364e-01 x0_1_TC=0.000e+00
+	dgs1=4.3e-07 dgs2=2.6e-13 dgs3=0.8 dgs4=0.23
+	ags1=(8.744e-10*si) ags2=(5.303e-10*si) ags3=1.606e+00 ags4=2.760e-01
+	ags5=(8.269e-11*si) ags6=-1.321e+01 ags7=-3.801e+00
+	agd1=(3.397e-15*sr) agd2=(-7.360e-12*sr) agd3=-2.703e+01 agd4=5.973e+00
+	agd5=(3.069e-13*sr) agd6=-1.755e+01 agd7=8.471e+00 agd8=(7.164e-12*sr)
+	agd9=-2.712e+01 agd10=5.941e+00
+	asd1=(4.392e-10*so) asd2=(7.925e-11*so) asd3=-1.844e+01 asd4=2.396e+00
+	asd5=(-3.743e-10*so) asd6=-2.110e+01 asd7=-1.795e+01 asd8=(-2.922e-10*so)
+	asd9=-2.353e+01 asd10=1.620e+01
+	rg_value=0.7

rd (drainin drain) resistor r=((1-rpara_s_factor)*rpara*(1-arTc*(temp-25)))
rs (sourcein source) resistor r=(rpara_s_factor*rpara*(1-arTc*(temp-25)))
rg (gatein gate) resistor r=(rg_value)

rcsdconv (drain source) resistor r=(1E9/aWg)
rcgsconv (gate source) resistor r=(1E9/aWg)
rcgdconv (gate drain) resistor r=(1E9/aWg)

gswitch drain source bsource i=( (v(drain,source)>0) ? 
+	(A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,source)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(drain,source)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,source))*v(drain,source))) :
+	(-A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,drain)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(source,drain)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,drain))*v(source,drain))) )

ggsdiode gate source bsource i=( (v(gate,source)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,source))/dgs3)-1)+dgs2*(exp((v(gate,source))/dgs4)-1))) )

ggddiode gate drain bsource i=( (v(gate,drain)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,drain))/dgs3)-1)+dgs2*(exp((v(gate,drain))/dgs4)-1))) )


C_GS  (gate source) bsource c=(ags1)  
G_CGS1 (gate source) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,source)-ags3)/ags4))+
+	ags5*ags7*log(1+exp((v(source,drain)-ags6)/ags7)) )

C_GD  (gate drain) bsource c=(agd1)  
G_CGD1 (gate drain) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,drain)-ags3)/ags4))+
+	agd2*agd4*log(1+exp((v(gate,drain)-agd3)/agd4))+
+	agd5*agd7*log(1+exp((v(gate,drain)-agd6)/agd7))+
+	agd8*agd10*log(1+exp((v(gate,drain)-agd9)/agd10)) )

C_SD  (source drain) bsource c=(asd1) 
G_CSD1 (source drain) bsource q=(asd2*asd4*log(1+exp((v(source,drain)-asd3)/asd4))+
+	asd5*asd7*log(1+exp((v(source,drain)-asd6)/asd7))+
+	asd8*asd10*log(1+exp((v(source,drain)-asd9)/asd10)) )

ends EPC2214
