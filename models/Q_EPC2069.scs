//* (C) Copyright Efficient Power Conversion Corporation. All rights reserved.
//*****************************************************************************
//* Version History:
//* 1.00: 09/21/2021 - Initial Model Creation

simulator lang=spectre

subckt EPC2069 (gatein drainin sourcein)
parameters  si=1.691e+00 so=9.440e+00 sr=2.818e+03
parameters  aWg=2818 A1=(1.145e-02*aWg) k2=2.300e+00 k3=9.000e-02 rpara=5.526e-04
+	rpara_s_factor=2.778e-01 aITc=3.000e-03 arTc=-8.360e-03 k2Tc=7.000e-04
+	x0_0=5.349e+00 x0_0_TC=-1.000e-03 x0_1=-9.948e-01 x0_1_TC=0.000e+00
+	dgs1=4.3e-07 dgs2=2.6e-13 dgs3=0.8 dgs4=0.23
+	ags1=(7.800e-10*si) ags2=(3.660e-10*si) ags3=2.048e+00 ags4=2.070e-01
+	ags5=(1.000e-12*si) ags6=1.000e+00 ags7=1.000e+00
+	agd1=(6.520e-15*sr) agd2=(1.090e-13*sr) agd3=-3.024e+00 agd4=4.061e+00
+	agd5=(7.140e-15*sr) agd6=-1.188e+01 agd7=3.001e+01 agd8=(1.450e-14*sr)
+	agd9=-1.400e+01 agd10=1.326e+00
+	asd1=(8.360e-10*so) asd2=(6.830e-11*so) asd3=-1.462e+01 asd4=2.047e+00
+	asd5=(-9.500e-10*so) asd6=2.141e+02 asd7=-1.919e+02 asd8=(1.530e-12*so)
+	asd9=-3.720e+01 asd10=-1.624e+00
+	rg_value=0.4

rd (drainin drain) resistor r=((1-rpara_s_factor)*rpara*(1-arTc*(temp-25)))
rs (sourcein source) resistor r=(rpara_s_factor*rpara*(1-arTc*(temp-25)))
rg (gatein gate) resistor r=(rg_value)

rcsdconv (drain source) resistor r=(1E9/aWg)
rcgsconv (gate source) resistor r=(1E9/aWg)
rcgdconv (gate drain) resistor r=(1E9/aWg)

gswitch drain source bsource i=( (v(drain,source)>0) ? 
+	(A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,source)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(drain,source)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,source))*v(drain,source))) :
+	(-A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,drain)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(source,drain)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,drain))*v(source,drain))) )

ggsdiode gate source bsource i=( (v(gate,source)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,source))/dgs3)-1)+dgs2*(exp((v(gate,source))/dgs4)-1))) )

ggddiode gate drain bsource i=( (v(gate,drain)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,drain))/dgs3)-1)+dgs2*(exp((v(gate,drain))/dgs4)-1))) )


C_GS  (gate source) bsource c=(ags1)  
G_CGS1 (gate source) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,source)-ags3)/ags4))+
+	ags5*ags7*log(1+exp((v(source,drain)-ags6)/ags7)) )

C_GD  (gate drain) bsource c=(agd1)  
G_CGD1 (gate drain) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,drain)-ags3)/ags4))+
+	agd2*agd4*log(1+exp((v(gate,drain)-agd3)/agd4))+
+	agd5*agd7*log(1+exp((v(gate,drain)-agd6)/agd7))+
+	agd8*agd10*log(1+exp((v(gate,drain)-agd9)/agd10)) )

C_SD  (source drain) bsource c=(asd1) 
G_CSD1 (source drain) bsource q=(asd2*asd4*log(1+exp((v(source,drain)-asd3)/asd4))+
+	asd5*asd7*log(1+exp((v(source,drain)-asd6)/asd7))+ 
+	asd8*asd10*log(1+exp((v(source,drain)-asd9)/asd10)) )

ends EPC2069
