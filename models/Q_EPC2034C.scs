//* (C) Copyright Efficient Power Conversion Corporation. All rights reserved.
//*****************************************************************************
//* Version History:
//* 1.00: 08/08/2019 - Initial Model Creation
//* -.--: 10/04/2022 - Fixed error

simulator lang=spectre

subckt EPC2034C (gatein drainin sourcein)
parameters  si=1.281e+00 so=1.089e+01 sr=1.423e+03
parameters  aWg=1423 A1=(2.850e-02*aWg) k2=1.962e+00 k3=9.000e-02 rpara=5.232e-03
+	rpara_s_factor=1.500e-01 aITc=4.000e-03 arTc=-6.900e-03 k2Tc=6.000e-04
+	x0_0=4.832e+00 x0_0_TC=-1.000e-03 x0_1=-3.638e-01 x0_1_TC=0.000e+00
+	dgs1=4.3e-07 dgs2=2.6e-13 dgs3=0.8 dgs4=0.23
+	ags1=(8.991e-10*si) ags2=(6.678e-10*si) ags3=1.561e+00 ags4=2.729e-01
+	ags5=(-7.601e-11*si) ags6=-1.662e+01 ags7=4.170e+00
+	agd1=(7.265e-16*sr) agd2=(8.490e-15*sr) agd3=-2.408e+01 agd4=4.730e+01
+	agd5=(2.464e-14*sr) agd6=-1.817e+01 agd7=1.301e+00 agd8=(6.554e-14*sr)
+	agd9=-9.938e+00 agd10=3.262e+00
+	asd1=(6.429e-11*so) asd2=(7.884e-11*so) asd3=-2.034e+01 asd4=1.602e+00
+	asd5=(5.058e-11*so) asd6=-7.564e+00 asd7=1.909e+01 asd8=(-2.469e-11*so)
+	asd9=-1.335e+02 asd10=-2.966e+01
+	rg_value=0.5

rd (drainin drain) resistor r=((1-rpara_s_factor)*rpara*(1-arTc*(temp-25)))
rs (sourcein source) resistor r=(rpara_s_factor*rpara*(1-arTc*(temp-25)))
rg (gatein gate) resistor r=(rg_value)

rcsdconv (drain source) resistor r=(1E9/aWg)
rcgsconv (gate source) resistor r=(1E9/aWg)
rcgdconv (gate drain) resistor r=(1E9/aWg)

gswitch drain source bsource i=( (v(drain,source)>0) ? 
+	(A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,source)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(drain,source)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,source))*v(drain,source))) :
+	(-A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,drain)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(source,drain)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,drain))*v(source,drain))) )

ggsdiode gate source bsource i=( (v(gate,source)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,source))/dgs3)-1)+dgs2*(exp((v(gate,source))/dgs4)-1))) )

ggddiode gate drain bsource i=( (v(gate,drain)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,drain))/dgs3)-1)+dgs2*(exp((v(gate,drain))/dgs4)-1))) )


C_GS  (gate source) bsource c=(ags1)  
G_CGS1 (gate source) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,source)-ags3)/ags4))+
+	ags5*ags7*log(1+exp((v(source,drain)-ags6)/ags7)) )

C_GD  (gate drain) bsource c=(agd1)  
G_CGD1 (gate drain) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,drain)-ags3)/ags4))+
+	agd2*agd4*log(1+exp((v(gate,drain)-agd3)/agd4))+
+	agd5*agd7*log(1+exp((v(gate,drain)-agd6)/agd7))+
+	agd8*agd10*log(1+exp((v(gate,drain)-agd9)/agd10)) )

C_SD  (source drain) bsource c=(asd1) 
G_CSD1 (source drain) bsource q=(asd2*asd4*log(1+exp((v(source,drain)-asd3)/asd4))+
+	asd5*asd7*log(1+exp((v(source,drain)-asd6)/asd7)) +
+	asd8*asd10*log(1+exp((v(source,drain)-asd9)/asd10)) )

ends EPC2034C
