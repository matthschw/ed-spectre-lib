*March 21, 2005
*Doc. ID: 77931, S-50383, Rev. B
*File Name: Si2343DS_ps.txt and Si2343DS_ps.lib

* added for compatibility (matthschw @ 04.10.22)
simulator lang=espice

.SUBCKT Si2343DS DRAIN GATE SOURCE
M1  3 GATE SOURCE SOURCE PMOS W=945229u L=0.25u          
M2  SOURCE GATE SOURCE DRAIN NMOS W=945229u L=0.55u
R1  DRAIN 3     RTEMP 19E-3
CGS GATE SOURCE     400E-12
DBD DRAIN SOURCE    DBD
**************************************************************************
.MODEL  PMOS       PMOS (  LEVEL  = 3               TOX    = 5E-8
+ RS     = 15.2E-3         RD     = 0               NSUB   = 11E16 
+ KP     = 4E-6            UO     = 400             
+ VMAX   = 0               XJ     = 5E-7            KAPPA  = 1E-2
+ ETA    = 1E-4            TPG    = -1  
+ IS     = 0               LD     = 0                        
+ CGSO   = 0               CGDO   = 0               CGBO   = 0 
+ NFS    = 0.8E12          DELTA  = 0.1)
*************************************************************************
.MODEL  NMOS       NMOS (  LEVEL  = 3               TOX    = 5E-8
+NSUB    = 7E16             NFS   = 10E11           TPG    = -1)   
*************************************************************************
.MODEL DBD D (CJO=100E-12 VJ=0.38 M=0.24
+RS=0.1 FC=0.5 IS=1E-12 TT=6E-8 N=1 BV=30.2)
*************************************************************************
.MODEL RTEMP RES (TC1=6.5E-3 TC2=5.5E-6)
*************************************************************************
.ENDS
