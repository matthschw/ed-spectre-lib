//* (C) Copyright Efficient Power Conversion Corporation. All rights reserved.
//*****************************************************************************
//* Version History:
//* 1.00: 12/31/2018 - Initial Model Creation
//* 1.01: 02/19/2019 - Updated the Capacitance Model
//* 1.02: 03/11/2019 - Updated the Model from the Preliminary Version
//* 1.03: 05/28/2019 - Fixed a typo
//* 1.04: 06/09/2019 - Updated the model

simulator lang=spectre

subckt EPC2052 (gatein drainin sourcein)
parameters  si=3.735e-01 so=1.942e+00 sr=4.980e+02
parameters  aWg=498 A1=(4.856e-02*aWg) k2=2.234e+00 k3=9.000e-02 rpara=8.507e-03
+	rpara_s_factor=2.700e-01 aITc=4.000e-03 arTc=-6.000e-03 k2Tc=5.000e-04
+	x0_0=4.031e+00 x0_0_TC=-1.000e-03 x0_1=4.070e-01 x0_1_TC=0.000e+00
+	dgs1=4.3e-07 dgs2=2.6e-13 dgs3=0.8 dgs4=0.23
+	ags1=(1.159e-09*si) ags2=(5.347e-10*si) ags3=1.652e+00 ags4=1.736e-01
+	ags5=(-1.229e-10*si) ags6=-4.954e+00 ags7=5.240e+00
+	agd1=(9.304e-15*sr) agd2=(4.571e-13*sr) agd3=1.107e+01 agd4=6.927e+00
+	agd5=(1.554e-14*sr) agd6=-1.010e+01 agd7=1.318e+00 agd8=(-6.839e-15*sr)
+	agd9=8.161e-10 agd10=-1.409e-01
+	asd1=(7.420e-11*so) asd2=(1.112e-10*so) asd3=-1.679e+01 asd4=6.946e+00
+	asd5=(4.849e-11*so) asd6=-1.237e+01 asd7=1.291e+00 asd8=(8.281e-12*so)
+	asd9=-7.423e+01 asd10=8.449e+00
+	rg_value=0.7

rd (drainin drain) resistor r=((1-rpara_s_factor)*rpara*(1-arTc*(temp-25)))
rs (sourcein source) resistor r=(rpara_s_factor*rpara*(1-arTc*(temp-25)))
rg (gatein gate) resistor r=(rg_value)

rcsdconv (drain source) resistor r=(1E9/aWg)
rcgsconv (gate source) resistor r=(1E9/aWg)
rcgdconv (gate drain) resistor r=(1E9/aWg)

gswitch drain source bsource i=( (v(drain,source)>0) ? 
+	(A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,source)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(drain,source)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,source))*v(drain,source))) :
+	(-A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,drain)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(source,drain)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,drain))*v(source,drain))) )

ggsdiode gate source bsource i=( (v(gate,source)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,source))/dgs3)-1)+dgs2*(exp((v(gate,source))/dgs4)-1))) )

ggddiode gate drain bsource i=( (v(gate,drain)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,drain))/dgs3)-1)+dgs2*(exp((v(gate,drain))/dgs4)-1))) )


C_GS  (gate source) bsource c=(ags1)  
G_CGS1 (gate source) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,source)-ags3)/ags4))+
+	ags5*ags7*log(1+exp((v(source,drain)-ags6)/ags7)) )

C_GD  (gate drain) bsource c=(agd1)  
G_CGD1 (gate drain) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,drain)-ags3)/ags4))+
+	agd2*agd4*log(1+exp((v(gate,drain)-agd3)/agd4))+
+	agd5*agd7*log(1+exp((v(gate,drain)-agd6)/agd7))+
+	agd8*agd10*log(1+exp((v(gate,drain)-agd9)/agd10)) )

C_SD  (source drain) bsource c=(asd1) 
G_CSD1 (source drain) bsource q=(asd2*asd4*log(1+exp((v(source,drain)-asd3)/asd4))+
+	asd5*asd7*log(1+exp((v(source,drain)-asd6)/asd7)) 
+	asd8*asd10*log(1+exp((v(source,drain)-asd9)/asd10)) )

ends EPC2052
