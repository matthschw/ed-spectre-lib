//* (C) Copyright Efficient Power Conversion Corporation. All rights reserved.
//*****************************************************************************
//* Version History:
//* 1.00: 12/31/2018 - Initial Model Creation
//* 1.01: 03/22/2019 - Updated the Model from the Preliminary Version
//* 1.02: 05/28/2019 - Fixed a typo
//* -.--: 10/04/2022 - Fixed error

simulator lang=spectre

subckt EPC2053 (gatein drainin sourcein)
parameters  aWg=1640 A1=(4.856e-02*aWg) k2=2.234e+00 k3=9.000e-02 rpara=2.661e-03
+	rpara_s_factor=2.700e-01 aITc=4.000e-03 arTc=-6.000e-03 k2Tc=5.000e-04
+	x0_0=4.031e+00 x0_0_TC=-1.000e-03 x0_1=4.070e-01 x0_1_TC=0.000e+00
+	dgs1=4.3e-07 dgs2=2.6e-13 dgs3=0.8 dgs4=0.23
+	ags1=(1.274e-09*si) ags2=(5.218e-10*si) ags3=1.691e+00 ags4=3.443e-01
+	ags5=(-6.418e-11*si) ags6=-1.055e+01 ags7=2.743e+00
+	agd1=(6.350e-15*sr) agd2=(-6.810e-15*sr) agd3=-1.836e+01 agd4=1.756e+00
+	agd5=(9.238e-14*sr) agd6=-6.892e+00 agd7=4.638e+00 agd8=(1.205e-15*sr)
+	agd9=-3.598e+01 agd10=2.158e+00
+	asd1=(7.655e-11*so) asd2=(5.144e-11*so) asd3=-1.294e+01 asd4=1.403e+00
+	asd5=(1.322e-08*so) asd6=8.952e+01 asd7=1.797e+01 asd8=(1.669e-11*so)
+	asd9=-1.782e+00 asd10=-1.264e+00
+	rg_value=0.6

rd (drainin drain) resistor r=((1-rpara_s_factor)*rpara*(1-arTc*(temp-25)))
rs (sourcein source) resistor r=(rpara_s_factor*rpara*(1-arTc*(temp-25)))
rg (gatein gate) resistor r=(rg_value)

rcsdconv (drain source) resistor r=(1E9/aWg)
rcgsconv (gate source) resistor r=(1E9/aWg)
rcgdconv (gate drain) resistor r=(1E9/aWg)

gswitch drain source bsource i=( (v(drain,source)>0) ? 
+	(A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,source)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(drain,source)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,source))*v(drain,source))) :
+	(-A1*(1-aITc*(temp-25))*log(1.0+exp((v(gate,drain)-(k2*(1-k2Tc*(temp-25))))/k3))* 
+	v(source,drain)/(1 + (x0_0*(1-x0_0_TC*(temp-25))+x0_1*(1-x0_1_TC*(temp-25))*v(gate,drain))*v(source,drain))) )

ggsdiode gate source bsource i=( (v(gate,source)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,source))/dgs3)-1)+dgs2*(exp((v(gate,source))/dgs4)-1))) )

ggddiode gate drain bsource i=( (v(gate,drain)>10) ? 
+	(0.125*aWg/1077*(dgs1*(exp((10.0)/dgs3)-1)+dgs2*(exp((10.0)/dgs4)-1))) : 
+	(0.125*aWg/1077*(dgs1*(exp((v(gate,drain))/dgs3)-1)+dgs2*(exp((v(gate,drain))/dgs4)-1))) )


C_GS  (gate source) bsource c=(ags1)  
G_CGS1 (gate source) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,source)-ags3)/ags4))+
+	ags5*ags7*log(1+exp((v(source,drain)-ags6)/ags7)) )

C_GD  (gate drain) bsource c=(agd1)  
G_CGD1 (gate drain) bsource q=(0.5*ags2*ags4*log(1+exp((v(gate,drain)-ags3)/ags4))+
+	agd2*agd4*log(1+exp((v(gate,drain)-agd3)/agd4))+
+	agd5*agd7*log(1+exp((v(gate,drain)-agd6)/agd7))+
+	agd8*agd10*log(1+exp((v(gate,drain)-agd9)/agd10)) )

C_SD  (source drain) bsource c=(asd1) 
G_CSD1 (source drain) bsource q=(asd2*asd4*log(1+exp((v(source,drain)-asd3)/asd4))+
+	asd5*asd7*log(1+exp((v(source,drain)-asd6)/asd7)) +
+	asd8*asd10*log(1+exp((v(source,drain)-asd9)/asd10)) )

ends EPC2053
